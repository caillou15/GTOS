#EV.DISPONIBLE(CLIC)
PROC/ EXP_IMG_RENOMMER(CLIC)
	REM/ ** EN DEVELOPPEMENT **
	REM/ Vous n'aurez plus a entrer le nom du fichier, mais par selection a la souris
	fix/ test_ex_fichier = -1
	fix/ CAP_NomFichier = 0
:DEBUT:
	rem/ Affichage du message si test -1 alors on affiche le premier message sinon le 2eme
	si/ %test_ex_fichier% = -1 (:
		MSGBOX/ /Texte=Entrez le nom du fichier a renommer /titre=(En developpement) Explorateur de fichiers %OS% /mode=3 /alerte=1 /nom=TEXTEMSG.EXP_FICHIER
	sinon/
		MSGBOX/ /Texte=Fichier introuvable!%CRLF%Entrez le nom du fichier a renommer /titre=Explorateur de fichiers %OS% (En developpement) /mode=3 /alerte=1 /nom=TEXTEMSG.EXP_FICHIER
	fin/ si

	
	REM/ Creation d'un evenement sur le Bouton OK du TextMsgbox
	REM/ %EXE_EN_COURS% permet d'utiliser le fichier en cours d'EXEcution
	INI/ BOUTON(
		INI;NOM = "BOKTXT.TEXTEMSG.EXP_FICHIER"
		INI;TYPE = "1"
		CREER/
		EV/ %EXE_EN_COURS%
	INI/ BOUTON)

	
	rem/ Et ... Attendre la fermeture de ce textemsgbox
	@#TESTPause pause/ /FERMER:TEXTEMSG.EXP_FICHIER
	rem/ La suite sera execute quand l'utilisateur aura clique sur le bouton OK (l'evenement BOKTXT.TEXTEMSG.EXP_FICHIER(CLIC))

	rem/ tester si l'utilisateur a entre du texte
	fix/ test_ex_fichier = /C LEN>%CAP_NomFichier%
	rem/ Si il a rien rentre alors on quitte
	si/ %test_ex_fichier% < 2 (:
		stop/
	Fin/ si

:Tester_fichier:
	REM/ Tester l'existence de ce fichier qui se trouve dans %TMP_ex_fichier% avec la fonction FEX (FichierEXiste)
	fix/ test = /C FEX>%EXP_CIBLE%\%CAP_NomFichier%
	SI/ %test% = 0 (:
		aller/ DEBUT
	SINON/
		REM/ Si le fichier existe alors on continue

		REM/ Affichage d'un TextMsgbox (mode 3)
		MSGBOX/ /Texte=Entrez le nouveau nom du fichier /titre=(En developpement) Renommage de %CAP_NomFichier% Explorateur de fichiers %OS% /mode=3 /alerte=1 /nom=TEXTEMSG.EXP_RENOMMER

		REM/ Creation d'un evenement sur le bouton OK du TextMsgbox
		REM/ %EXE_EN_COURS% permet d'utiliser le fichier en cours d'EXEcution
		INI/ BOUTON(
			INI;NOM = "BOKTXT.TEXTEMSG.EXP_RENOMMER"
			CREER/
			EV/ %EXE_EN_COURS%
		INI/ BOUTON)
	FIN/ SI
FIN/ PROC

REM/ Si l'utilisateur clique sur le bouton OK du MSGBOX (TEXTEMSG.EXP_RENOMMER)
PROC/ BOKTXT.TEXTEMSG.EXP_RENOMMER(CLIC)
	REM/ Recuperer le nom du dossier a partir du textemsgbox
	INI/ TEXTEBOX(
		INI;NOM = "TXTB.TEXTEMSG.EXP_RENOMMER"
		INI;TEXTE = "#%CAP_NouveauNomFichier"
	INI/ TEXTEBOX)
	
	REM/ On crée le repertoire
	RENOMMER/ %EXP_CIBLE%\%CAP_NomFichier% %EXP_CIBLE%\%CAP_NouveauNomFichier%

	REM/ Et on ferme le texteMsgbox
	Fermer/ TEXTEMSG.EXP_RENOMMER
FIN/ PROC

REM/ Si l'utilisateur clique sur le bouton OK du MSGBOX (TEXTEMSG.EXP_FICHIER)
PROC/ BOKTXT.TEXTEMSG.EXP_FICHIER(CLIC)
	REM/ Recuperer le nom entre dans le textebox et on le place dans %CAP_NomFichier%
	INI/ TEXTEBOX(
		INI;NOM = "TXTB.TEXTEMSG.EXP_FICHIER"
		INI;TEXTE = "#%CAP_NomFichier"
	INI/ TEXTEBOX)
	
	rem/ et on ferme ce processus!
	fermer/ TEXTEMSG.EXP_FICHIER
FIN/ PROC